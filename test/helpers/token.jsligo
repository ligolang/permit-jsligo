#import "../../src/main.jsligo" "Token"
#import "./assert.jsligo" "Assert"

// Some types for readability
type taddr = typed_address<parameter_of Token, Token.storage>;
type contr = contract<parameter_of Token>;
export type originated = {
    addr: address,
    taddr: taddr,
    //owners: address list;
    contr: contr
};

type initial_extended_storage = {
    admin : address,
    counter : nat,
    defaultExpiry : nat,
    maxExpiry : nat,
    permits : Token.Extension.permits,
    userExpiries : Token.Extension.userExpiries,
    permitExpiries : Token.Extension.permitExpiries,
    extension : Token.Extension.TokenTotalSupply.t
};

// Some dummy values when they don't matter for a given test
export const dummyDefaultExpiry = 0n;
export const dummyMaxExpiry = 0n;

export const getInitialExtendedStorage = (admin: address, initDefaultExpiry: nat, initMaxExpiry: nat) : initial_extended_storage => {
    return {
        admin : admin,
        counter : 0n,
        defaultExpiry : initDefaultExpiry,
        maxExpiry : initMaxExpiry,
        permits : (Big_map.empty as Token.Extension.permits),
        userExpiries : (Big_map.empty as Token.Extension.userExpiries),
        permitExpiries : (Big_map.empty as Token.Extension.permitExpiries),
        extension : (Big_map.empty as Token.TokenTotalSupply.t)
    };
};

// Originate a Token contract with given init_storage storage
export const originate = (init_storage: Token.storage) : originated => {
    let orig = Test.originate(contract_of(Token), init_storage, 0tez);
    let contr = Test.to_contract (orig.addr);
    let addr = Test.to_address (orig.addr);
    return { addr : addr, taddr : orig.addr, contr : contr };
};

export const originateFromFile = (init_storage : Token.storage) : originated => {
    let f = "../../src/main.jsligo";
    let orig = Test.originate_from_file(f, init_storage, 0tez);
    let contr = Test.to_contract (orig.addr);
    let addr = Test.to_address (orig.addr);
    return {addr : addr, taddr : orig.addr , contr : contr}
};


// Make a permit with given packed params and secret key
// The chain_id is equal to 0x00000000 in the test framework
export const makePermit = (hash_: bytes, account: [address, key, string], token_addr: address, counter: nat) : Token.permitParams => {
    let [_, pubKey, secretKey] = account;
    let packed = Bytes.pack ([[0x00000000, token_addr], [counter, hash_]]);
    let sig = Test.sign(secretKey, packed);
    return [pubKey, [sig, hash_]]
};

// Call entry point of Token contr contract
const call = (p: parameter_of Token, contr: contr) : test_exec_result =>
    Test.transfer_to_contract(contr, p, 0mutez)

export const permit = (p: list<Token.permitParams>, contr: contr) : test_exec_result =>
    call(Permit(p), contr)

export const setExpiry = (p: Token.expiryParams, contr: contr) : test_exec_result =>
    call(SetExpiry(p), contr)

export const setAdmin = (p: address, contr: contr) : test_exec_result =>
    call(SetAdmin(p), contr)

export const transfer = (p: Token.FA2.transfer, contr: contr) : test_exec_result => {
    let param : parameter_of Token = Transfer(p);
    return call(param, contr)
};

export const createToken = (md: Token.FA2.TokenMetadata.data, owner: address, amount: nat, contr: contr) : test_exec_result =>
    call(Create_token([md, owner, amount]), contr);

export const mintToken = (p: list<Token.mintOrBurn>, contr: contr) : test_exec_result =>
    call(Mint_token(p), contr)

export const burnToken = (p: list<Token.mintOrBurn>, contr: contr) : test_exec_result =>
    call(Burn_token(p), contr)

export const permitSuccess = (p: list<Token.permitParams>, contr: contr) : unit =>
    Assert.tx_success (permit(p, contr))

export const setExpirySuccess = (p: Token.expiryParams, contr: contr) : unit =>
    Assert.tx_success (setExpiry(p, contr))

export const setAdminSuccess = (p: address, contr: contr) : unit =>
    Assert.tx_success (setAdmin(p, contr))

export const transferSuccess = (p: Token.FA2.transfer, contr: contr) : unit =>
    Assert.tx_success (transfer(p, contr))

export const createTokenSuccess = (md: Token.FA2.TokenMetadata.data, owner: address, amount: nat, contr: contr) : unit =>
    Assert.tx_success (createToken(md, owner, amount, contr))

export const mintTokenSuccess = (p: list<Token.mintOrBurn>, contr: contr) : unit =>
    Assert.tx_success (mintToken(p, contr))

export const burnTokenSuccess = (p: list<Token.mintOrBurn>, contr: contr) : unit => {
    Assert.tx_success (burnToken(p, contr));
};

// Assert Token contract at [taddr] has permit with [address, hash] key
export const assertHasPermit = (taddr: taddr, paramPermitKey: Token.Extension.permitKey) : unit => {
    let s = Test.get_storage (taddr);
    return match (Big_map.find_opt(paramPermitKey, s.extension.permits)) {
        when(Some(_)): unit;
        when(None()): Test.failwith("Permits Big_map key should not be missing");
    }
};

// Assert Token contract at [taddr] has no permit with [address, hash] key
export const assertNoPermit = (taddr: taddr, paramPermitKey: Token.Extension.permitKey) : unit => {
    let s = Test.get_storage (taddr);
    return match (Big_map.find_opt(paramPermitKey, s.extension.permits)) {
        when(Some(_)): Test.failwith("Permits Big_map key should be None");
        when(None()): unit;
    }
};

// Assert Token contract at [taddr] has counter equals to [counter]
export const assertCounter = (taddr: taddr, counter: nat) : unit => {
    let s = Test.get_storage (taddr);
    assert_with_error( (s.extension.counter == counter), "Counter does not have expected value");
};

// Assert Token contract at [taddr] has user expiry for [userAddr] equal to [seconds]
export const assertUserExpiry = (taddr: taddr, userAddr: address, seconds: option<nat>) : unit => {
    let s = Test.get_storage (taddr);
    return match (Big_map.find_opt(userAddr, s.extension.userExpiries)) {
        when(Some(s)): assert (s == seconds);
        when(None()): Test.failwith("User epiries Big_map key should not be missing");
    }
};

// Assert Token contract at [taddr] has user permit expiry for [userAddr] and
// hash [hash_] equal to [seconds]
export const assertPermitExpiry = (taddr: taddr, userAddr: address, hash_: bytes, seconds: option<nat>) : unit => {
    let s = Test.get_storage (taddr);
    return match (Big_map.find_opt([userAddr, hash_], s.extension.permitExpiries)) {
        when(Some(s)): assert (s == seconds);
        when(None()): Test.failwith("Permit epiries Big_map key should not be missing");
    }
};

// assert Token contract at [taddr] have [owner] address with [amount_] tokens in its ledger
export const assertBalance = (taddr: taddr, owner: address, tokenId: nat, amount_: nat) : unit => {
    let s = Test.get_storage (taddr);
    return match (Big_map.find_opt([owner, tokenId], s.ledger)) {
        when(Some(tokens)): assert(tokens == amount_);
        when(None()): Test.failwith("Big_map key should not be missing");
    }
};

// assert Token contract at [taddr] have tokenTotalSupply for [tokenId] matching [amount_]
export const assertSupply = (taddr: taddr, tokenId: Token.FA2.Ledger.token_id, amount_: nat) : unit => {
    let s = Test.get_storage (taddr);
    return match (Big_map.find_opt(tokenId, s.extension.extension)) {
        when(Some(tokens)): assert(tokens == amount_);
        when(None()): Test.failwith("Big_map key should not be missing");
    }
};
